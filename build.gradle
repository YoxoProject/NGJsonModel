plugins {
    id 'java'
    id 'maven-publish'
    id 'co.uzzu.dotenv.gradle' version '2.0.0'
}

group = 'fr.romaindu35'
version = '1.6.0'

// Chargement des variables d'environnement
def envFile = file('.env')
Properties properties = new Properties()
if (envFile.exists()) {
    envFile.withInputStream { properties.load(it) }
}

def getEnvOrProperty = { String name ->
    System.getenv(name) ?: properties.getProperty(name)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_1_8 // Car doit pouvoir être utilisé avec un mod Forge 1.6.4 (Java 7)
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.13.1"
    implementation "org.tukaani:xz:1.9"
    implementation "com.github.luben:zstd-jni:1.5.6-3"

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'NGJsonModel'
                description = 'Une librairie Java pour manipuler les données de NationsGlory et permettre leur export.'
                url = 'https://github.com/YoxoProject/NGJsonModel'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Romaindu35'
                        name = 'Romain'
                        email = 'contact@romaindu35.fr'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/YoxoProject/NGJsonModel.git'
                    developerConnection = 'scm:git:ssh://github.com:YoxoProject/NGJsonModel.git'
                    url = 'https://github.com/YoxoProject/NGJsonModel'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'Romaindu35_Maven'
            url = "https://maven.romaindu35.fr/releases/"
            credentials {
                username = getEnvOrProperty('MAVEN_USERNAME')
                password = getEnvOrProperty('MAVEN_PASSWORD')
            }
        }
    }
}

task runBenchmark(type: JavaExec) {
    group = "Verification"
    description = "Runs the compression benchmark"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "fr.romaindu35.benchmark.CompressionBenchmark"
    workingDir = file(".")
}
